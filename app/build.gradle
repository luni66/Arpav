apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
    signingConfigs {
        release {
            afterEvaluate { project ->
                def propsFile = rootProject.file('keystore.properties')
                def configName = 'release'

                if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
                    def props = new Properties()
                    props.load(new FileInputStream(propsFile))
                    android.signingConfigs[configName].storeFile = file(props['storeFile'])
                    android.signingConfigs[configName].storePassword = props['storePassword']
                    android.signingConfigs[configName].keyAlias = props['keyAlias']
                    android.signingConfigs[configName].keyPassword = props['keyPassword']
                }
            }
        }
    }

    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "eu.lucazanini.arpav"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 3
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-compat:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.volley:volley:1.0.0'
    implementation 'com.android.support:animated-vector-drawable:27.1.1'
    implementation 'com.android.support:mediarouter-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.code.gson:gson:2.8.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    def acraVersion = '5.1.3'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "ch.acra:acra-core:$acraVersion"
    implementation "ch.acra:acra-dialog:$acraVersion"
    implementation "ch.acra:acra-notification:$acraVersion"
    implementation "ch.acra:acra-toast:$acraVersion"
}
